// Game.jack
// Controla la l√≥gica del juego: spawn, score, vidas y loop principal.

class Game {

    field Player player;
    field Falling obj;
    field int score;
    field int lives;
    field int seed;
    field int lastSpawnX;

    static String scoreLabel;
    static String livesLabel;

    constructor Game new() {
        let player = Player.new();
        let seed = 123;
        let score = 0;
        let lives = 3;
        let lastSpawnX = -1;

        // objeto inicial (fruta)
        let obj = Falling.new(240, 0);

        // HUD
        let scoreLabel = String.new(7);
        do scoreLabel.appendChar(83);
        do scoreLabel.appendChar(99);
        do scoreLabel.appendChar(111);
        do scoreLabel.appendChar(114);
        do scoreLabel.appendChar(101);
        do scoreLabel.appendChar(58);
        do scoreLabel.appendChar(32);

        let livesLabel = String.new(7);
        do livesLabel.appendChar(76);
        do livesLabel.appendChar(105);
        do livesLabel.appendChar(118);
        do livesLabel.appendChar(101);
        do livesLabel.appendChar(115);
        do livesLabel.appendChar(58);
        do livesLabel.appendChar(32);

        return this;
    }

    method int rand(int range) {
        var int r;

        if (range < 1) { return 0; }

        let seed = seed * 25173 + 13849;
        let seed = seed & 32767;

        let r = seed;

        while (r > range) {
            let r = r - range;
        }

        return r;
    }

    method void drawHUD() {
        do Output.moveCursor(0, 0);
        do Output.printString(scoreLabel);
        do Output.printInt(score);
        do Output.printString(livesLabel);
        do Output.printInt(lives);
        return;
    }

    method void run() {
        var char key;
        var int res;
        var int spawnX;
        var boolean same;
        var int objType;

        while (true) {

            do player.move();

            // -------------------
            // Movimiento
            // -------------------
            let res = obj.fall();

            if (res = 1) {

                if (obj.getType() = 0) {
                    let lives = lives - 1;

                    if (lives = 0) {
                        do Screen.clearScreen();
                        do Output.moveCursor(10, 10);
                        do Output.printString("GAME OVER");
                        do Output.moveCursor(11, 10);
                        do Output.printString("Score: ");
                        do Output.printInt(score);
                        return;
                    }
                }

                let spawnX = rand(460);

                let same = (spawnX = lastSpawnX);
                if (same) {
                    let spawnX = spawnX + 20;
                    if (spawnX > 460) { let spawnX = spawnX - 40; }
                }

                let lastSpawnX = spawnX;

                // =============================
                // CAMBIO: probabilidad 1 de 3
                // =============================
                let objType = 0;
                if (rand(3) = 1) { let objType = 1; }

                do obj.reset(spawnX, objType);
            }

            // -------------------
            // Captura
            // -------------------
            if (obj.isCaught(player.getX(), player.getWidth())) {

                if (obj.getType() = 0) {
                    let score = score + 1;
                } else {
                    let lives = lives - 1;

                    if (lives = 0) {
                        do Screen.clearScreen();
                        do Output.moveCursor(10, 10);
                        do Output.printString("GAME OVER");
                        do Output.moveCursor(11, 10);
                        do Output.printString("Score: ");
                        do Output.printInt(score);
                        return;
                    }
                }

                let spawnX = rand(460);

                let same = (spawnX = lastSpawnX);
                if (same) {
                    let spawnX = spawnX + 20;
                    if (spawnX > 460) { let spawnX = spawnX - 40; }
                }

                let lastSpawnX = spawnX;

                // =============================
                // CAMBIO: probabilidad 1 de 3
                // =============================
                let objType = 0;
                if (rand(3) = 1) { let objType = 1; }

                do obj.reset(spawnX, objType);
            }

            do drawHUD();

            let key = Keyboard.keyPressed();
            if (key = 140) {
                do player.dispose();
                do Screen.clearScreen();
                return;
            }

            do Sys.wait(1);
        }

        return;
    }

}
